
#Область ОписаниеПеременных

&НаСервере
Перем мОбъектОтчета;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания,
ФормаДлительнойОперации;

&НаСервере
Перем СохраненныеДанныеОтчета;

&НаСервере
Перем СтруктураМногострочныхЧастей Экспорт; 

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обход ошибки платформы Параметр сеанса отсутствует или удален.
	ТабличныйДокумент.Очистить();
	
	СформироватьСтруктуруРеквизитовФормы();
	
	// Параметр ЭтоНовыйОтчет служит для определения был ли создан отчет копированием 
	// Если отчет создан копированием ЭтоНовыйОтчет = Ложь
	СтруктураРеквизитовФормы.ЭтоНовыйОтчет = ?(ЗначениеЗаполнено(Параметры.мСохраненныйДок), Ложь, Истина);
	СтруктураРеквизитовФормы.мСохраненныйДок = Параметры.мСохраненныйДок;
	
	СтруктураРеквизитовФормы.мВерсияФормы = "01/01/2024";
	
	СтруктураРеквизитовФормы.мВыбраннаяФорма          = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мПериодичность           = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма        = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.Организация              = Параметры.Организация;
	
	СтруктураМногострочныхЧастей = Новый Структура;
	
	СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
		= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, УникальныйИдентификатор);
		
	СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию = Новый Соответствие;

	// >> таб_#33613
	таб_XBRLОбщегоНазначения.ПриСозданииНаСервере(ЭтаФорма); 
    // << таб_
	Инициализация(Параметры.БезОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти  

#Область Инициализация

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	// >> таб_#56564
	//СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный";
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> "" Тогда
		СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный";
	КонецЕсли;	
	// << таб_#56564
	
	ТабличныйДокумент.Вывести(Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы,
	".Форма.") - 1), 7)].ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы,
	"ФормаОтчета")) + "_" + СтруктураРеквизитовФормы.НаимТекущегоРаздела));
	
	ИнициализацияЗаполнитьСтруктурыДанных();
		
	ИнициализироватьОписанияМногострочныхЧастей();
	
	ИнициализироватьРазделы();
	
	ФормироватьСтруктуруСтраницОтчета();
	
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	НомерКорректировки = 1;
	
	СформироватьДеревоРазделовОтчетаНаСервере();
	
	ЗаполнитьЗначенияПоУмолчанию = Ложь;
	ОтчетСкопированОрганизацияНеИзменилась = Ложь;
	ИсходныйОтчетЗаПервыйКвартал = Ложь;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			ВосстановитьСохраненныеДанные();
		Иначе
			//СтруктураРеквизитовФормы.ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			//СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 0;
			ЗаполнитьЗначенияПоУмолчанию = Истина;
			КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
		КонецЕсли;
		Модифицированность = Истина;
	Иначе
		ВосстановитьСохраненныеДанные();
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			ИсходныйОтчетЗаПервыйКвартал
			= (Месяц(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания) = 3);
			Если СтруктураРеквизитовФормы.мСохраненныйДок.Организация
				= СтруктураРеквизитовФормы.Организация Тогда
				ОтчетСкопированОрганизацияНеИзменилась = Истина;
			КонецЕсли;
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
			Модифицированность = Истина;
		КонецЕсли;
		Если НЕ БезОткрытияФормы И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
			ЗаблокироватьДанныеДляРедактирования(
			СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда	 
		ЗаполнениеШапкиНаСервере();
		СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	КонецЕсли;
	
	таб_XBRLОбщегоНазначения.УстановитьФорматВыводаНаСервере(ЭтаФорма);
		
	ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
	
	// >> таб_#56564
	Если  СтруктураРеквизитовФормы.СфераПрименения = "ССД" Или СтруктураРеквизитовФормы.СфераПрименения = "ОВС" Тогда
		ЭтаФорма.Заголовок = "Форма по ОКУД 0420166";
	ИначеЕсли СтруктураРеквизитовФормы.СфераПрименения = "УК" Или СтруктураРеквизитовФормы.СфераПрименения = "АИФ" Тогда
		ЭтаФорма.Заголовок = "Форма по ОКУД 0420515";
	ИначеЕсли СтруктураРеквизитовФормы.СфераПрименения = "ПУРЦБ" Тогда
		ЭтаФорма.Заголовок = "Форма по ОКУД 0420428";
	ИначеЕсли СтруктураРеквизитовФормы.СфераПрименения = "НПФ" Тогда
		ЭтаФорма.Заголовок = "Форма по ОКУД 0420268";
	КонецЕсли;	
	// << таб_#56564
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьРазделы()
	
	// Формирование структуры свойств многоуровневых разделов.
	//
	мСтруктураРазделов = Новый Структура;
	
	СтруктураРаздела = Новый Структура;
	
	// >> таб_#56564
	//мСтруктураРазделов.Вставить("Титульный", СтруктураРаздела);
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		мСтруктураРазделов.Вставить("Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения, СтруктураРаздела);
	Иначе
		мСтруктураРазделов.Вставить("Титульный", СтруктураРаздела);
	КонецЕсли;	
	// << таб_#56564
	
	СтруктураРаздела = Новый Структура;
	мСтруктураРазделов.Вставить("Раздел1", СтруктураРаздела);
	
	СтруктураРаздела = Новый Структура;
	мСтруктураРазделов.Вставить("Комментарии", СтруктураРаздела);
		
	// Формирование начальной структуры разделов
	// с заполнением пустыми данными.
	//
	СохрТаблДокумент = Новый ТабличныйДокумент;
	СохрТаблДокумент.Вывести(ТабличныйДокумент);
	
	Для каждого Элем Из мСтруктураРазделов Цикл	
		ТабличныйДокумент.Очистить();
		Если  Элем.Ключ <> "Комментарии" Тогда
			ТабличныйДокумент.Вывести(Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы,
			СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы,
			СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + Элем.Ключ));
			
			СохранитьДанныеРаздела(Элем.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(СохрТаблДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчета() Экспорт
	
	мДеревоСтраницОтчета.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	// >> таб_#56564
	//СтрокаУровня1.ИмяСтраницы        = "Титульный";
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		СтрокаУровня1.ИмяСтраницы        =  "Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		СтрокаУровня1.ИмяСтраницы        = "Титульный";
	КонецЕсли;	
	// << таб_#56564
	СтрокаУровня1.Представление      = "Титульный";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ПоказатьСтраницу	 = 1;

	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Раздел1";
	СтрокаУровня1.Представление      = "Раздел 1";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ПоказатьСтраницу	 = 1;
			
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = Новый Структура;
	СтруктураРеквизитовФормы.Вставить("НаимТекущегоРаздела");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы");
	СтруктураРеквизитовФормы.Вставить("мВерсияФормы");
	СтруктураРеквизитовФормы.Вставить("мЗаписываетсяНовыйДокумент");
	СтруктураРеквизитовФормы.Вставить("АдресВременногоХранилищаРасшифровки");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");
	СтруктураРеквизитовФормы.Вставить("ВидДокумента");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("мВариант");
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("ГруппаОрганизаций", Новый СписокЗначений);
	СтруктураРеквизитовФормы.Вставить("НаимОрганизации");
	СтруктураРеквизитовФормы.Вставить("ТекущаяСтрокаРазделовОтчета");
	СтруктураРеквизитовФормы.Вставить("ИдентификаторЗадания");
	СтруктураРеквизитовФормы.Вставить("АдресВоВременномХранилище");
	СтруктураРеквизитовФормы.Вставить("АдресВоВремХранилищеСтруктураМногострочныхЧастей");
	СтруктураРеквизитовФормы.Вставить("СоответствиеПоказателейМногострочныхЧастейИхОписанию");
	
	СтруктураРеквизитовФормы.Вставить("СтруктураМногострочныхЧастей");
	СтруктураРеквизитовФормы.Вставить("ВыводитьСтраницуВТаблДокНаСервере");
	СтруктураРеквизитовФормы.Вставить("СфераДеятельности", "ПУРЦБ");
	СтруктураРеквизитовФормы.Вставить("ЭтоНовыйОтчет", Ложь); 
	
	СтруктураРеквизитовФормы.Вставить("СфераПрименения");  // таб_#56564

КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоРазделовОтчетаНаСервере()
	
	ЭтаФорма["РазделыОтчета"].ПолучитьЭлементы().Очистить();
	
	Для Каждого ЭлементДереваСтраницОтчета Из ЭтаФорма["мДеревоСтраницОтчета"].ПолучитьЭлементы() Цикл
		Если ЭлементДереваСтраницОтчета.ПоказатьСтраницу = 1 Тогда	
			ЭлементРазделовОтчета = ЭтаФорма["РазделыОтчета"].ПолучитьЭлементы().Добавить();
			ЭлементРазделовОтчета.КолонкаРазделыОтчета         = ЭлементДереваСтраницОтчета.Представление;
			ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			
			ИмяРаздела = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОписанияМногострочныхЧастей()
	
	ИмяМакетаСтруктурыМногострочныхЧастей = "СтруктураМногострочныхЧастей2024Кв1";
	ТекстОписанияСтруктуры = ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(ИмяМакетаСтруктурыМногострочныхЧастей).ПолучитьТекст();
	ИнициализироватьОписанияМногострочныхПоXML(ТекстОписанияСтруктуры);
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	// Заполнение соответствия показателей и их описаний в структуре
	// для возможности получения описания по имени показателя.
	Для Каждого МногострочнаяЧасть Из СтруктураМногострочныхЧастей Цикл
		
		ИдентификаторМногострочнойЧасти = МногострочнаяЧасть.Ключ;
		ПоказателиМногострочнойЧасти    = МногострочнаяЧасть.Значение.Состав;
		
		Если ТипЗнч(ПоказателиМногострочнойЧасти) <> Тип("Массив") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Показатель Из ПоказателиМногострочнойЧасти[0] Цикл
			СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию.Вставить(Показатель.Ключ, ИдентификаторМногострочнойЧасти);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОписанияМногострочныхПоXML(ТекстXML);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстXML);
	ПостроительDOM = Новый ПостроительDOM;
	
	КорневойУзел = ПостроительDOM.Прочитать(ЧтениеXML);
	
	Для Каждого Узел Из КорневойУзел.ДочерниеУзлы Цикл
		
		Если Узел.ИмяУзла <> "МногострочныеЧасти" Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого УзелМногострочныхЧастей Из Узел.ДочерниеУзлы Цикл
			ИмяРазделаМногострочныхЧастей = ПолучитьАтрибутКакСтроку(УзелМногострочныхЧастей, "ИмяРаздела");
			Если ИспользоватьОписаниеМногострочнойЧасти(ИмяРазделаМногострочныхЧастей) Тогда
				ВнестиОписаниеМногострочныхЧастейРаздела(УзелМногострочныхЧастей);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ИспользоватьОписаниеМногострочнойЧасти(ИмяОписанияМногострочныхЧастей)
	
	Результат = Ложь;
	
	Для Каждого Отчет Из СтруктураОтчета Цикл
		Если Отчет.Значение.ИмяОписанияМногострочныхЧастей = ИмяОписанияМногострочныхЧастей Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ВнестиОписаниеМногострочныхЧастейРаздела(УзелРаздела)
	
	ОписаниеТиповЧисло  = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2));
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	ОписаниеТиповДата	= Новый ОписаниеТипов("Дата", , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ИмяПоляТабличногоДокумента = СтрЗаменить(УзелРаздела.Атрибуты.ПолучитьИменованныйЭлемент("РасположениеРаздела").Значение, "ПолеТабличногоДокумента", "");
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	Если СтруктураМногострочныхЧастей = Неопределено Тогда
		СтруктураМногострочныхЧастей = Новый Структура;
	КонецЕсли;
	
	Для Каждого Узел Из УзелРаздела.ДочерниеУзлы Цикл
		
		Если Узел.ИмяУзла <> "МногострочнаяЧасть" Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторМногострочнойЧасти = ПолучитьАтрибутКакСтроку(Узел, "Идентификатор");
		МинимальноеКоличествоСтрок  = ПолучитьАтрибутКакЧисло(Узел, "МинимальноеКоличествоСтрок");
		МаксимальноеКоличествоСтрок = ПолучитьАтрибутКакЧисло(Узел, "МаксимальноеКоличествоСтрок");
		ВерхнийЭлементСтроки = ПолучитьАтрибутКакСтроку(Узел, "ВерхнийЭлементСтроки");
		НижнийЭлементСтроки  = ПолучитьАтрибутКакСтроку(Узел, "НижнийЭлементСтроки");
		
		СтруктураМногострочнойЧасти = Новый Структура;
		СтруктураМногострочнойЧасти.Вставить("Масштаб", Новый Структура("МинимальноеКоличествоСтрок, МаксимальноеКоличествоСтрок", МинимальноеКоличествоСтрок, МаксимальноеКоличествоСтрок));
		СтруктураМногострочнойЧасти.Вставить("Габариты", Новый Структура("ВерхнийЭлементСтроки, НижнийЭлементСтроки", ВерхнийЭлементСтроки, НижнийЭлементСтроки));
		СтруктураМногострочнойЧасти.Вставить("ИмяПоляТабличногоДокумента", ИмяПоляТабличногоДокумента);
		
		Для Каждого ДочернийУзел Из Узел.ДочерниеУзлы Цикл
			
			Если ДочернийУзел.ИмяУзла = "Кодификаторы" Тогда
				ВнестиОписаниеКодификаторовМногострочнойЧасти(ДочернийУзел, СтруктураМногострочнойЧасти);
				
			ИначеЕсли ДочернийУзел.ИмяУзла = "Заголовки" Тогда
				ВнестиОписаниеЗаголовковМногострочнойЧасти(ДочернийУзел, СтруктураМногострочнойЧасти);
				
			ИначеЕсли ДочернийУзел.ИмяУзла = "Состав" Тогда
				ВнестиОписаниеСоставаМногострочнойЧасти(ДочернийУзел, СтруктураМногострочнойЧасти);
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтруктураМногострочныхЧастей.Вставить(ИдентификаторМногострочнойЧасти, СтруктураМногострочнойЧасти);
		
	КонецЦикла;
	
	// >> таб_#11520
	Если СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
		СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
			= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, Новый УникальныйИдентификатор);
	Иначе
		СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
			= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, УникальныйИдентификатор);
	КонецЕсли;
	// << таб_
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияЗаполнитьСтруктурыДанных(СохраненныеДанные = Неопределено)
	
	СтруктураОтчета = Новый Структура;
	
	// Раздел 1
	ЭлементСтруктураОтчета = Новый Структура();
	ЭлементСтруктураОтчета.Вставить("ИмяСтраницы",                    "Раздел1");
	ЭлементСтруктураОтчета.Вставить("ИмяМакета",                      "Раздел1");
	ЭлементСтруктураОтчета.Вставить("ИмяОписанияМногострочныхЧастей", "Раздел1");
	
	СтруктураОтчета.Вставить("Раздел1", ЭлементСтруктураОтчета);
	
	
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		ТитульныйЛист = "Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		ТитульныйЛист = "Титульный";
	КонецЕсли;		
	
	ЭлементСтруктураОтчета = Новый Структура();
	ЭлементСтруктураОтчета.Вставить("ИмяСтраницы",                    ТитульныйЛист);
	ЭлементСтруктураОтчета.Вставить("ИмяМакета",                      ТитульныйЛист);
	ЭлементСтруктураОтчета.Вставить("ИмяОписанияМногострочныхЧастей", "");
	
	СтруктураОтчета.Вставить(ТитульныйЛист, ЭлементСтруктураОтчета);
	// << таб_#56564
КонецПроцедуры

&НаСервере
Процедура ВнестиОписаниеКодификаторовМногострочнойЧасти(Узел, СтруктураМногострочнойЧасти)
	
	МассивКодификаторов = Новый Массив;
	
	Для Каждого ДочернийУзел Из Узел.ДочерниеУзлы Цикл
		Если ДочернийУзел.ИмяУзла <> "Кодификатор" Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьКода = ПолучитьАтрибутКакСтроку(ДочернийУзел, "ОбластьКода");
		НачальныйКод = ПолучитьАтрибутКакЧисло(ДочернийУзел, "НачальныйКод");
		
		МассивКодификаторов.Добавить(Новый Структура("Область, Код", ОбластьКода, НачальныйКод));
		
	КонецЦикла;
	
	Если МассивКодификаторов.Количество() > 0 Тогда
		СтруктураМногострочнойЧасти.Вставить("Кодификаторы", МассивКодификаторов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВнестиОписаниеЗаголовковМногострочнойЧасти(Узел, СтруктураМногострочнойЧасти)
	
	МассивЗаголовков = Новый Массив;
	
	Для Каждого ДочернийУзел Из Узел.ДочерниеУзлы Цикл
		Если ДочернийУзел.ИмяУзла <> "Заголовок" Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьЗаголовка = ПолучитьАтрибутКакСтроку(ДочернийУзел, "ОбластьЗаголовка");
		ШаблонЗаголовка = ПолучитьАтрибутКакСтроку(ДочернийУзел, "ШаблонЗаголовка");
		
		МассивЗаголовков.Добавить(Новый Структура("Область, Заголовок", ОбластьЗаголовка, ШаблонЗаголовка));
		
	КонецЦикла;
	
	Если МассивЗаголовков.Количество() > 0 Тогда
		СтруктураМногострочнойЧасти.Вставить("Заголовки", МассивЗаголовков);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВнестиОписаниеСоставаМногострочнойЧасти(Узел, СтруктураМногострочнойЧасти)
		
	ТаблицаСостава = Новый Массив;
	СтруктураСостава = Новый Структура;
	
	Для Каждого ДочернийУзел Из Узел.ДочерниеУзлы Цикл
		
		Если ДочернийУзел.ИмяУзла <> "ЭлементСтроки" Тогда
			Продолжить;
		КонецЕсли;
		
		Область = ПолучитьАтрибутКакСтроку(ДочернийУзел, "Область");
		
		СтруктураСостава.Вставить(Область);
		
	КонецЦикла;
	
	НачальноеКоличествоСтрок = СтруктураМногострочнойЧасти.Масштаб.МинимальноеКоличествоСтрок;
	Для Инд = 1 По НачальноеКоличествоСтрок Цикл
		ТаблицаСостава.Добавить(СтруктураСостава);
	КонецЦикла;
	
	СтруктураМногострочнойЧасти.Вставить("Состав", ТаблицаСостава);
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеДанныхОтчета

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(ЭтаФорма, "", Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		НаименованиеТекущегоРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	Иначе
		НаименованиеТекущегоРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	КонецЕсли;
	
	РезультатСохранения = ПередСохранением(ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаименованиеТекущегоРаздела);
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует);
		
	Иначе
		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = "";
	КПП = "";
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует)
	
	Если Ложь Тогда // #41363 Убрал вопрос при сохранении
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
		|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение", Автосохранение, Вариант, ВыполняемоеОповещение);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.Открыть();
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, Вариант, Неопределено, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
	Иначе
		Возврат;
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен, Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
		Оповестить("ОбновитьФормуXBRL", СтруктураРеквизитовФормы.мСохраненныйДок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПередСохранением(ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, НаимТекРаздела)
	
	Если РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтаФорма, "", "") Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И ((СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) ИЛИ (СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено) ИЛИ (Вариант <> СтруктураРеквизитовФормы.мВариант)) Тогда
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			
		ИначеЕсли Вариант <> СтруктураРеквизитовФормы.мВариант Тогда
			
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПодобныйОтчетСуществует
		ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Сохранить(Автосохранение, Вариант, , НаимТекРаздела);
	
КонецФункции

&НаСервере
Функция Сохранить(Автосохранение = Ложь, Вариант, ВидДокументаНомерКорректировкиИзменен = Неопределено, НаимТекРаздела) Экспорт	
	
	ДанныеДляРазблокирования = Неопределено;
	
	Если НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
			ДанныеДляРазблокирования = Новый Структура("Ключ,ИдФормы",
			СтруктураРеквизитовФормы.мСохраненныйДок, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчетаНаСервере(
		ЭтаФорма, "", Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен,
		СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ИсточникОтчета           = Метаданные.Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].Имя;
	СтруктураРеквизитовФормы.мСохраненныйДок.НаименованиеОтчета       = Метаданные.Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].ОсновнаяФорма.Синоним;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала               = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВыбраннаяФорма           = СтруктураРеквизитовФормы.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок.Организация              = СтруктураРеквизитовФормы.Организация;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи              = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий              = Комментарий;
	СтруктураРеквизитовФормы.мСохраненныйДок.Вид                      = Вариант;
	СтруктураРеквизитовФормы.мСохраненныйДок.ПредставлениеВида        = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(СтруктураРеквизитовФормы.мСохраненныйДок.Вид);
	СтруктураРеквизитовФормы.мВариант                                 = Вариант;
	
	СтруктураРеквизитовФормы.мСохраненныйДок.таб_СфераПрименения      = СтруктураРеквизитовФормы.СфераПрименения;  // таб_#56564
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ФорматВыгрузки = "Автоматически";
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
		СохранитьДанныеРаздела(НаимТекРаздела);
	КонецЕсли;
	
	ПоказателиОтчета = Новый Структура();
	
	Для Каждого Раздел Из мСтруктураРазделов Цикл
		
		Если Раздел.Ключ = "Комментарии" Тогда
			мСтруктураРазделов[Раздел.Ключ] = Новый Структура;
			Для Каждого ЭлемТК ИЗ ТаблицаКомментарии Цикл
				мСтруктураРазделов[Раздел.Ключ].Вставить("footnote" + "_" + ЭлемТК.Ячейка, ЭлемТК.Комментарий);	
			КонецЦикла;
		КонецЕсли;
		
		ПоказателиОтчета.Вставить(Раздел.Ключ, мСтруктураРазделов[Раздел.Ключ]);
		
	КонецЦикла;
	
	СписокСохранения = Новый Структура();
	
	//СписокСохранения.Вставить("ГруппаОрганизаций", СтруктураРеквизитовФормы.ГруппаОрганизаций);
	
	Если СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки <> Неопределено Тогда
		ТаблицаРасшифровки = ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
		
		СписокСохранения.Вставить("ТаблицаРасшифровки", ТаблицаРасшифровки);
		
	КонецЕсли;
	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	СписокСохранения.Вставить("ВерсияФормы", СтруктураРеквизитовФормы.мВерсияФормы);
	// >> таб_#33613
	СписокСохранения.Вставить("ВерсияТаксономии", ЭтаФорма.Элементы.ВерсияТаксономии.Заголовок);
    // << таб_	
	СохрСтруктураМногострочныхЧастей = Новый Структура;
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	Для Каждого МногострочнаяЧасть Из СтруктураМногострочныхЧастей Цикл
		
		НовыйЭлементСтруктуры = Новый Структура;
		
		Для Каждого ЭлементСтруктуры Из МногострочнаяЧасть.Значение Цикл
			
			Если ЭлементСтруктуры.Ключ = "ИмяПоляТабличногоДокумента" Тогда
				
				НовыйЭлементСтруктуры.Вставить(ЭлементСтруктуры.Ключ, "ПолеТабличногоДокумента" + ЭлементСтруктуры.Значение);
				
			ИначеЕсли ЭлементСтруктуры.Ключ = "Состав" Тогда	
				
				ТаблЗнач = Новый ТаблицаЗначений;
				
				Колонки = ЭлементСтруктуры.Значение[0];
				
				Для Каждого Колонка Из Колонки Цикл
					
					ТаблЗнач.Колонки.Добавить(Колонка.Ключ);
					
				КонецЦикла;
				
				Для Каждого Элемент Из ЭлементСтруктуры.Значение Цикл
					
					СтрТаблЗнач = ТаблЗнач.Добавить();
					
					Для Каждого ЗначениеКолонки Из Элемент Цикл
						
						СтрТаблЗнач[ЗначениеКолонки.Ключ] = ЗначениеКолонки.Значение;
						
					КонецЦикла;
					
				КонецЦикла;
				
				НовыйЭлементСтруктуры.Вставить(ЭлементСтруктуры.Ключ, ТаблЗнач);
				
			Иначе 
				
				НовыйЭлементСтруктуры.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		СохрСтруктураМногострочныхЧастей.Вставить(МногострочнаяЧасть.Ключ, НовыйЭлементСтруктуры);
		
	КонецЦикла;
	
	СписокСохранения.Вставить("СтруктураМногострочныхЧастей", СохрСтруктураМногострочныхЧастей);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	Попытка
		СтруктураРеквизитовФормы.мСохраненныйДок.Записать();
	Исключение
		Если НЕ Автосохранение Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"),
			РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки()));
			Сообщение.Сообщить();
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецПопытки;
	
	СохранитьДеревоСтраницОтчета(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	Модифицированность = Ложь;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы
		И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
	Если ДанныеДляРазблокирования <> Неопределено Тогда
		РазблокироватьДанныеДляРедактирования(ДанныеДляРазблокирования.Ключ, ДанныеДляРазблокирования.ИдФормы);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура СохранитьДеревоСтраницОтчета(Знач мСохраненныйДок)
	
	Если мСохраненныйДок <> Неопределено Тогда
		Если ТипЗнч(мСохраненныйДок) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			мСохраненныйДок = мСохраненныйДок.ПолучитьОбъект();
		КонецЕсли;
		Если Не мСохраненныйДок.ЭтоНовый() Тогда
			// Сохраняем выбранные листы для печати в хранилище.
			ХранилищеДанных = Новый ХранилищеЗначения(РеквизитФормыВЗначение("мДеревоСтраницОтчета"));
			мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
			Попытка
				мСохраненныйДок.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВосстановлениеСохраненныхДанныхОтчета

&НаСервере
Функция СохраненныеДанныеОтчета() Экспорт
	
	Если СохраненныеДанныеОтчета = Неопределено Тогда
		СохраненныеДанныеОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
		
	КонецЕсли;
	
	Возврат СохраненныеДанныеОтчета;
	
КонецФункции

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	Перем ТаблицаРасшифровки;
	Перем ПоказателиОтчета;
	Перем Автозаполнение;
	Перем СохраненнаяСтруктураМногострочныхЧастей;
	
	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		СтруктураРеквизитовФормы.Организация = СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения;
	ДатаПодписи = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	Комментарий = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;
	
	СписокСохранения = СохраненныеДанныеОтчета();
	
	Если НЕ СписокСохранения.Свойство("ОкружениеСохранения") Тогда
		ДеревоНастройкиСтраниц = СтруктураРеквизитовФормы.мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
		ЗначениеВРеквизитФормы(ДеревоНастройкиСтраниц.Скопировать(), "мДеревоСтраницОтчета");
	КонецЕсли;
	
	СписокСохранения.Свойство("ГруппаОрганизаций", СтруктураРеквизитовФормы.ГруппаОрганизаций);
	Если СтруктураРеквизитовФормы.ГруппаОрганизаций = Неопределено Тогда
		СтруктураРеквизитовФормы.ГруппаОрганизаций = Новый СписокЗначений;
	КонецЕсли;
	
	Если СписокСохранения.Свойство("ТаблицаРасшифровки", ТаблицаРасшифровки) Тогда
		СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки = ПоместитьВоВременноеХранилище(ТаблицаРасшифровки, Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
	СписокСохранения.Свойство("СтруктураМногострочныхЧастей", СохраненнаяСтруктураМногострочныхЧастей);
	
	Если СохраненнаяСтруктураМногострочныхЧастей <> Неопределено Тогда
		
		Для Каждого ЭлементСтруктуры Из СохраненнаяСтруктураМногострочныхЧастей Цикл
			
			ТаблЗнач = ЭлементСтруктуры.Значение.Состав;
			
			СтруктураЗнач = Новый Структура;
			МассивЗнач    = Новый Массив;
			
			Для Каждого СтрТаблЗнач Из ТаблЗнач Цикл
				
				СтруктураЗнач.Очистить();
				
				Для Каждого Колонка Из ТаблЗнач.Колонки Цикл
					
					СтруктураЗнач.Вставить(Колонка.Имя, СтрТаблЗнач[Колонка.Имя]);
					
				КонецЦикла;
				
				МассивЗнач.Добавить(СтруктураЗнач);
				
			КонецЦикла;
			
			ЭлементСтруктуры.Значение.Состав = МассивЗнач;
			
			ЭлементСтруктуры.Значение.ИмяПоляТабличногоДокумента = СтрЗаменить(ЭлементСтруктуры.Значение.ИмяПоляТабличногоДокумента, "ПолеТабличногоДокумента", "");
			
			
		КонецЦикла;
		
		КонвертироватьСтруктуруМногострочныхЧастей(СохраненнаяСтруктураМногострочныхЧастей);
		
		СтруктураМногострочныхЧастей = СохраненнаяСтруктураМногострочныхЧастей;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
		= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, УникальныйИдентификатор);
		
	СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
	
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		
		мСтруктураРазделов.Вставить(Раздел.Ключ, ПоказателиОтчета[Раздел.Ключ]);
		
		Если  Раздел.Ключ = "Комментарии" Тогда
			ТаблицаКомментарии.Очистить();
			Для каждого ЭлемСтр Из Раздел.Значение Цикл
				НоваяСтрока = ТаблицаКомментарии.Добавить();
				НоваяСтрока.Ячейка = Прав(ЭлемСтр.Ключ,СтрДлина(ЭлемСтр.Ключ) - 9);
				НоваяСтрока.Комментарий = ЭлемСтр.Значение;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	//СформироватьДеревоРазделовОтчетаНаСервере();
		
	// >> таб_#56564	
	//ИмяТекРаздела = "Титульный";
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		ИмяТекРаздела = "Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		ИмяТекРаздела = "Титульный";
	КонецЕсли;		
	// << таб_#56564
	
	ТабличныйДокумент.Вывести(Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы,
	".Форма.") - 1), 7)].ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы,
	"ФормаОтчета")) + "_" + ИмяТекРаздела));
	
	Для Каждого ЭлементСтруктуры Из СтруктураМногострочныхЧастей Цикл
		
		Если ЭлементСтруктуры.Значение.ИмяПоляТабличногоДокумента = ИмяТекРаздела Тогда
			ВывестиРазделВТабличныйДокумент(ЭлементСтруктуры.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлСтруктуры Из мСтруктураРазделов[ИмяТекРаздела] Цикл
		Попытка
			ТабличныйДокумент.Области[ЭлСтруктуры.Ключ].Значение = ЭлСтруктуры.Значение;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);
	
	таб_XBRLОбщегоНазначения.УстановитьФорматВыводаНаСервере(ЭтаФорма);
	
	КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаСобытийВТабличномДокументе

&НаКлиенте
Процедура ТабличныйДокументВыбор(Элемент, Область, СтандартнаяОбработка)  
	
	СписокСправочников = Новый Соответствие;
	
	// >> таб_#56564
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		СфераПрименения = СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		СфераПрименения = "ПУРЦБ";
	КонецЕсли;		
	// << таб_#56564
	
	Если СтрЧислоВхождений(Область.Имя, "ДобавитьСтроку") = 1 И Область.Текст <> "" Тогда
		СтандартнаяОбработка = Ложь;
		ДобавитьСтроку("");
		
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "УдалитьСтроку") = 1 И Область.Текст <> "" Тогда
		СтандартнаяОбработка = Ложь;
		УдалитьСтроку("");
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "П042042800100000000000110") = 1 Тогда	
		СтандартнаяОбработка = Ложь;
		Значение = Новый СписокЗначений;
		СписокЗначение = таб_XBRLОбщегоНазначения.ПолучитьСписок("Vidotchetnosti_audit_proverke_Enumerator60", СфераПрименения);
		Для Каждого Элем Из СписокЗначение Цикл
			Значение.Добавить(Элем.Значение, Элем.Значение);	
		КонецЦикла;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьИзСпискаЗавершение", ЭтотОбъект, Область.Имя);
		ПоказатьВыборИзСписка(ОписаниеОповещения, Значение);
		Возврат;
		
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "П042042800200000000000005_1") = 1 Тогда	
		
		СтандартнаяОбработка = Ложь;
		
		СписокЗначение = таб_XBRLОбщегоНазначения.ПолучитьСписок("Priznak_Nulevogo_OtchetaEnumerator40", СфераПрименения);
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьИзСпискаЗавершение", ЭтотОбъект, Область.Имя);
		ПоказатьВыборИзСписка(ОписаниеОповещения, СписокЗначение);
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ИмяРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	УказанныйАдрес = Результат;
	
	Если УказанныйАдрес = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Область.Значение = ?(УказанныйАдрес.Название = "Адрес не указывается", "", УказанныйАдрес.Название);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзСпискаЗавершение(ВыбранноеЗначение, Ячейка) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		ТабличныйДокумент.Области[Ячейка].Значение = ВыбранноеЗначение.Представление;
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПомещениеДанныхИзТабличногоДокументаВМестаХранения

&НаСервере
Процедура СохранитьДанныеРаздела(НаимРаздела, ВеткаРаздела = Неопределено)
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела <> "Комментарии" Тогда	
		
		ЭтаФорма.мСтруктураРазделов[НаимРаздела] = СобратьДанныеТекущегоТаблПоля(ЭтаФорма, "ТабличныйДокумент");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеТабличногоДокумента(НаимРаздела)
	
	ДанныеТаблДокумента = Новый Структура;
	
	Для Инд = 0 По ТабличныйДокумент.Области.Количество() - 1 Цикл
		
		ТекОбласть = ТабличныйДокумент.Области[Инд];
		
		Если СтрЧислоВхождений(Тип(ТекОбласть), "Рисунок") = 0
			И ТекОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И ТекОбласть.СодержитЗначение = Истина Тогда
			
			ДанныеТаблДокумента.Вставить(ТекОбласть.Имя, ТекОбласть.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеТаблДокумента;
	
КонецФункции

#КонецОбласти

#Область ПомещениеДанныхИзМестХраненияВТабличныйДокумент

&НаСервере
Процедура ВывестиДанныеВТабличныйДокументНаСервере(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела = Неопределено)
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	ВывестиДанныеВТабличныйДокумент(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела);
	
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяТекРаздела;
	
	//ПоказатьПериод(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеВТабличныйДокумент(ИмяТекРаздела, НовыеНомераСтрокМногоуровнегоРаздела = Неопределено, ИмяЯчейки = Неопределено, Эксель = Ложь) Экспорт
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбъектОтчета(ЭтаФорма.ИмяФормы).ПолучитьМакет(Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, "ФормаОтчета")) + "_" + ?(ИмяТекРаздела = "ФормаОтчета", СтруктураОтчета.ОПУ.ИмяМакета, СтруктураОтчета[ИмяТекРаздела].ИмяМакета)));
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	Для Каждого ЭлементСтруктуры Из СтруктураМногострочныхЧастей Цикл
		Если ЭлементСтруктуры.Значение.ИмяПоляТабличногоДокумента = ИмяТекРаздела Тогда
			ВывестиРазделВТабличныйДокумент(ЭлементСтруктуры.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлСтруктуры Из ЭтаФорма.мСтруктураРазделов[ИмяТекРаздела] Цикл
		Обл = ТабличныйДокумент.Области.Найти(ЭлСтруктуры.Ключ);
		Если Обл <> Неопределено И Обл.СодержитЗначение Тогда 
			Обл.Значение = ЭлСтруктуры.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементСтруктуры Из СтруктураМногострочныхЧастей Цикл
		ПеренумероватьСтрокиИЗаполнитьЗаголовкиМногострочнойЧасти(ЭлементСтруктуры.Значение, ИмяТекРаздела)
	КонецЦикла;
	
	таб_XBRLОбщегоНазначения.УстановитьФорматВыводаНаСервере(ЭтаФорма);
	
	ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
	
	Если Эксель Тогда
		таб_XBRLОбщегоНазначения.УбратьТехническиеЯчейкиИзВыгрузкиВЭксель(ТабличныйДокумент, СтруктураМногострочныхЧастей);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматВыводаНаСервере()
	
	мСтрокаФормата = "ЧЦ = 25; ЧДЦ = 2; ЧРД=,; ЧН=-; ЧО=1";
	
	Для Каждого ТекущаяОбласть Из ТабличныйДокумент.Области Цикл
		// Для возможности отделять монетарные данные от не монетарных
		// установлен признак "РазмерКартинки = Черепица" в макете
		Если Строка(ТекущаяОбласть.РазмерКартинки) <> "Черепица"
			И ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И ТекущаяОбласть.СодержитЗначение = Истина 
			И Строка(ТекущаяОбласть.ТипЗначения) = "Число" Тогда
			ТекущаяОбласть.Формат = мСтрокаФормата;
		КонецЕсли;
		
		Если ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И ТекущаяОбласть.СодержитЗначение = Истина
			И ТекущаяОбласть.Защита = Ложь Тогда
			
			Если ТекущаяОбласть.ТипЗначения.СодержитТип(Тип("Число"))  Тогда
				ТекущаяОбласть.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			Иначе
				ТекущаяОбласть.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
			КонецЕсли;
			
			ТекущаяОбласть.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Авто;
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ИзменениеТабличногоДокумента   

&НаСервере
Процедура ВставитьДополнительнуюСтроку()
	
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ТекущаяОбласть;
	Если СтрНайти(ТекущаяОбласть.Имя, "ДобавитьСтроку") = 1 Тогда 
		ИмяПоказателя = СтрЗаменить(ТекущаяОбласть.Имя, "ДобавитьСтрокуЗначок_", "");
		ИмяПоказателя = СтрЗаменить(ИмяПоказателя, "ДобавитьСтроку_", "");
	Иначе
		ИмяПоказателя = ИмяПоказателяБезСчетчика(ТекущаяОбласть.Имя);
	КонецЕсли;
	
	ВставитьДополнительнуюСтрокуПоСистемеБухОтчетности(ИмяПоказателя);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьДополнительнуюСтрокуПоСистемеБухОтчетности(ИмяПоказателя)
	
	// Идентификатор многострочной части должен совпадать с именем области многострочной части в таблице.
	ИдентификаторМногострочнойЧасти = СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию[ИмяПоказателя];
	
	Попытка
		
		ПолучитьСтруктуруМногострочныхЧастей();
		
		ОписаниеМногострочнойЧасти = СтруктураМногострочныхЧастей[ИдентификаторМногострочнойЧасти];
		
	Исключение
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не выбран многострочный раздел, в который необходимо добавить строку.'");
		Сообщение.Сообщить();
		Возврат;
		
	КонецПопытки;
	
	// Если количество строк достигло предела, то добавления не производится.
	КоличествоСтрокВМногострочнойЧасти = ОписаниеМногострочнойЧасти.Состав.Количество();
	Если ЛОЖЬ И ОписаниеМногострочнойЧасти.Масштаб.МаксимальноеКоличествоСтрок <> 0	
	   И КоличествоСтрокВМногострочнойЧасти >= ОписаниеМногострочнойЧасти.Масштаб.МаксимальноеКоличествоСтрок Тогда
		
		Сообщение = Новый СообщениеПользователю;

		Сообщение.Текст = НСтр("ru='Невозможно добавить строку многострочного раздела: достигнуто максимальное количество.'");

		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	// Очищаем именованную область многострочной части и сохраняем исходную позицию.
	ИмяОбластиМногострочнойЧастиВТаблице = ИдентификаторМногострочнойЧасти;
		
	Если ТабличныйДокумент.Области.Найти(ИмяОбластиМногострочнойЧастиВТаблице) <> Неопределено Тогда
		ИсходнаяОбластьСтрокВерх = ТабличныйДокумент.Области[ИмяОбластиМногострочнойЧастиВТаблице].Верх;
		ИсходнаяОбластьСтрокНиз  = ТабличныйДокумент.Области[ИмяОбластиМногострочнойЧастиВТаблице].Низ;
		ТабличныйДокумент.Области[ИмяОбластиМногострочнойЧастиВТаблице].Имя = "";
	Иначе
		ИсходнаяОбластьСтрокВерх = 0;
		ИсходнаяОбластьСтрокНиз  = 0;
	КонецЕсли;
	
	// За основу для копирования используется последняя строка многострочного раздела.
	ИмяВерхнегоЭлементаСтроки = ОписаниеМногострочнойЧасти.Габариты.ВерхнийЭлементСтроки + "_" + СтрокаЧГ0(КоличествоСтрокВМногострочнойЧасти);
	ИмяНижнегоЭлементаСтроки  = ОписаниеМногострочнойЧасти.Габариты.НижнийЭлементСтроки + "_" + СтрокаЧГ0(КоличествоСтрокВМногострочнойЧасти);
	
	ОбластьВставляемойСтрокиВерх = ТабличныйДокумент.Области[ИмяВерхнегоЭлементаСтроки].Верх;
	ОбластьВставляемойСтрокиНиз  = ТабличныйДокумент.Области[ИмяНижнегоЭлементаСтроки].Низ;
	
	ОбластьВставляемойСтроки = ТабличныйДокумент.Область(ОбластьВставляемойСтрокиВерх, , ОбластьВставляемойСтрокиНиз, );
	
	// Добавляем строку к макету и назначаем имена областям показателей.
	ТабличныйДокумент.ВставитьОбласть(ОбластьВставляемойСтроки, , ТипСмещенияТабличногоДокумента.ПоВертикали, Ложь);
	
	ВысотаСтроки = ОбластьВставляемойСтрокиНиз - ОбластьВставляемойСтрокиВерх + 1;
	Смещение = ВысотаСтроки * КоличествоСтрокВМногострочнойЧасти;
	
	СтруктураЭлементовМногострочнойЧасти = Новый Структура;
	
	ОсноваИмениПоказателяДляДопСтроки = "";
	Для Каждого Элемент Из ОписаниеМногострочнойЧасти.Состав[0] Цикл
		ОсноваИмениПоказателя = Элемент.Ключ;
		Если Лев(ОсноваИмениПоказателя, 1) = "П" И Прав(ОсноваИмениПоказателя, 2) = "01" Тогда 
			ОсноваИмениПоказателяДляДопСтроки = ОсноваИмениПоказателя;
		КонецЕсли;
		
		СтруктураЭлементовМногострочнойЧасти.Вставить(ОсноваИмениПоказателя);
		
		ИсходнаяОбластьПоказателя = ТабличныйДокумент.Области[ОсноваИмениПоказателя + "_1"];
		
		ОбластьПоказателяНовойСтроки = ТабличныйДокумент.Область(ИсходнаяОбластьПоказателя.Верх + Смещение, ИсходнаяОбластьПоказателя.Лево,
		                                                               ИсходнаяОбластьПоказателя.Низ + Смещение, ИсходнаяОбластьПоказателя.Право);
		ИмяОбластиНовойСтроки = ОсноваИмениПоказателя + "_" + СтрокаЧГ0(КоличествоСтрокВМногострочнойЧасти + 1);
		
		ОбластьПоказателяНовойСтроки.Имя = ИмяОбластиНовойСтроки;
		
		Если ТабличныйДокумент.Области[ИмяОбластиНовойСтроки].СодержитЗначение Тогда
			ОбластьПоказателяНовойСтроки.Значение = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
	// Назначаем новую именнованную область многострочной части.
	Если ИсходнаяОбластьСтрокВерх <> 0 Тогда
		ОбластьДляИменования = ТабличныйДокумент.Область(ИсходнаяОбластьСтрокВерх, , ИсходнаяОбластьСтрокНиз + ВысотаСтроки, );
		ОбластьДляИменования.Имя = ИмяОбластиМногострочнойЧастиВТаблице;
		ОбластьУдалитьНовая = ТабличныйДокумент.Область(ИсходнаяОбластьСтрокНиз + 1, 2, ИсходнаяОбластьСтрокНиз + 1, 2);
		ОбластьУдалитьНовая.Имя = "УдалитьСтроку" + ОсноваИмениПоказателяДляДопСтроки  + "_" + СтрокаЧГ0(КоличествоСтрокВМногострочнойЧасти + 1);
	Иначе 
		Верх = ОбластьВставляемойСтрокиВерх;
		Шаг = ОбластьВставляемойСтрокиНиз - ОбластьВставляемойСтрокиВерх + 1;
		ОбластьУдалитьНовая = ТабличныйДокумент.Область(Верх + Шаг, 2, Верх + Шаг, 2);
		ОбластьУдалитьНовая.Имя = "УдалитьСтроку" + ОсноваИмениПоказателяДляДопСтроки  + "_" + СтрокаЧГ0(ОписаниеМногострочнойЧасти.Состав.Количество() + 1);
	КонецЕсли;
	
	// Зафиксируем увеличение многострочной части в структуре описаний.
	ОписаниеМногострочнойЧасти.Состав.Добавить(СтруктураЭлементовМногострочнойЧасти);
	
	ПеренумероватьСтрокиИЗаполнитьЗаголовкиМногострочнойЧасти(ОписаниеМногострочнойЧасти, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	// Установим первое поле добавленной строки текущим.
	НомерПоследнейСтроки = ОписаниеМногострочнойЧасти.Состав.Количество();
	ИмяОбластиДляФокусаВвода = ИмяПоказателя + "_" + СтрокаЧГ0(НомерПоследнейСтроки);
	Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области[ИмяОбластиДляФокусаВвода];
	
	ТекущийЭлемент = Элементы.ТабличныйДокумент;
	ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
	
	СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
		= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, УникальныйИдентификатор);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОтрисоватьЗначкиУдаленияСтрок(ТабДок)
	
	МассивОбластейСДопСтроками = Новый Массив;
	
	Для Каждого ОбластьТД Из ТабДок.Области Цикл
		Если СтрЧислоВхождений(ОбластьТД.Имя, "УдалитьСтроку") > 0 Тогда
			Если СтрЧислоВхождений(ОбластьТД.Имя, "_2") > 0 Тогда
				МассивОбластейСДопСтроками.Добавить(Лев(ОбластьТД.Имя, СтрНайти(ОбластьТД.Имя, "_") - 1));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбластьТД Из ТабДок.Области Цикл
		Если СтрЧислоВхождений(ОбластьТД.Имя, "УдалитьСтроку") > 0 Тогда
			Если МассивОбластейСДопСтроками.Найти(Лев(ОбластьТД.Имя, СтрНайти(ОбластьТД.Имя, "_") - 1)) = Неопределено Тогда
				ОбластьТД.Гиперссылка = Ложь;
				ОбластьТД.Текст = "";
			Иначе
				ОбластьТД.Гиперссылка = Истина;
				ОбластьТД.Текст = "х";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ИмяПоказателя = ДополнительныеПараметры.ИмяПоказателя;
	ИмяПоказателя = СтрЗаменить(ИмяПоказателя, "УдалитьСтроку", "");
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьДополнительнуюСтроку(ИмяПоказателя);
	
	//Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "..." Тогда
	//	РасчетНаКлиенте(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтроку(ИмяПоказателя)
	
	УдалитьДополнительнуюСтрокуПосистемеБухОтчетности(ИмяПоказателя);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтрокуПосистемеБухОтчетности(ИмяПоказателя)
	
	// Идентификатор многострочной части должен совпадать с областью многострочной части в таблице.
	ИмяПоказателяБезСчетчика = ИмяПоказателяБезСчетчика(ИмяПоказателя);
	
	ИдентификаторМногострочнойЧасти = СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию[ИмяПоказателяБезСчетчика];
	
	Если ИдентификаторМногострочнойЧасти = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не выбрана удаляемая строка многострочного раздела.'");
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	СтруктураМногострочнойЧасти = СтруктураМногострочныхЧастей[ИдентификаторМногострочнойЧасти];
	
	// Номер строки удаляемой многострочной части определим по имени выделенной ячейки.
	НомерУдаляемойСтроки = Число(Сред(ИмяПоказателя, СтрНайти(ИмяПоказателя, "_") + 1));
	ИндексСтрокиТаблицы = НомерУдаляемойСтроки - 1;
	
	Если СтруктураМногострочнойЧасти.Состав.Количество() > СтруктураМногострочнойЧасти.Масштаб.МинимальноеКоличествоСтрок Тогда
		
		ПеремещатьФокусВвода = (НомерУдаляемойСтроки = СтруктураМногострочнойЧасти.Состав.Количество());
		
		УдаляемаяОбластьВерх = ТабличныйДокумент.Области[СтруктураМногострочнойЧасти.Габариты.ВерхнийЭлементСтроки + "_" + СтрокаЧГ0(НомерУдаляемойСтроки)].Верх;
		УдаляемаяОбластьНиз  = ТабличныйДокумент.Области[СтруктураМногострочнойЧасти.Габариты.НижнийЭлементСтроки  + "_" + СтрокаЧГ0(НомерУдаляемойСтроки)].Низ;
		
		ВысотаСтроки = УдаляемаяОбластьНиз - УдаляемаяОбластьВерх + 1;
		УдаляемаяОбласть = ТабличныйДокумент.Область(УдаляемаяОбластьВерх, , УдаляемаяОбластьНиз, );
		ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		// Переименование областей многострочной части.
		Для Инд = НомерУдаляемойСтроки + 1 По СтруктураМногострочнойЧасти.Состав.Количество() Цикл
			Для Каждого Элемент Из СтруктураМногострочнойЧасти.Состав[0] Цикл
				ОсноваИмениОбласти = Элемент.Ключ;
				Если Прав(ОсноваИмениОбласти, 2) = "01" И Лев(ОсноваИмениОбласти, 1) = "П" Тогда 
					ПереименуемаяОбласть = ТабличныйДокумент.Области["УдалитьСтроку" + ОсноваИмениОбласти + "_" + СтрокаЧГ0(Инд)];
					ПереименуемаяОбласть.Имя = "УдалитьСтроку" + ОсноваИмениОбласти + "_" + СтрокаЧГ0(Инд - 1);
				КонецЕсли;
				ПереименуемаяОбласть = ТабличныйДокумент.Области[ОсноваИмениОбласти + "_" + СтрокаЧГ0(Инд)];
				ПереименуемаяОбласть.Имя = ОсноваИмениОбласти + "_" + СтрокаЧГ0(Инд - 1);
			КонецЦикла;
		КонецЦикла;
		
		СтруктураМногострочнойЧасти.Состав.Удалить(ИндексСтрокиТаблицы);
		
		Если ПеремещатьФокусВвода Тогда
			// Установим первое поле добавленной строки текущим.
			НомерПоследнейСтроки = СтруктураМногострочнойЧасти.Состав.Количество();
			ИмяБудущейТекущейОбласти = ИмяПоказателяБезСчетчика + "_" + СтрокаЧГ0(НомерПоследнейСтроки);
			Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области[ИмяБудущейТекущейОбласти];
		КонецЕсли;
		
	Иначе
		Для Инд = 1 По СтруктураМногострочнойЧасти.Масштаб.МинимальноеКоличествоСтрок Цикл
			Для Каждого Элемент Из СтруктураМногострочнойЧасти.Состав[0] Цикл
				ИмяОчищаемойОбласти = Элемент.Ключ + "_" + СтрокаЧГ0(Инд);
				ОчищаемаяОбласть = ТабличныйДокумент.Области[ИмяОчищаемойОбласти];
				Если ОчищаемаяОбласть.СодержитЗначение Тогда
					Если ТипЗнч(ОчищаемаяОбласть.Значение) = Тип("Строка") Тогда
						ОчищаемаяОбласть.Значение = "";
					Иначе
						ОчищаемаяОбласть.Значение = 0;
					КонецЕсли;
				Иначе
					ОчищаемаяОбласть.Текст = "";
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	ПеренумероватьСтрокиИЗаполнитьЗаголовкиМногострочнойЧасти(СтруктураМногострочнойЧасти, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	ОтрисоватьЗначкиУдаленияСтрок(ТабличныйДокумент);
	ТекущийЭлемент = Элементы.ТабличныйДокумент;
	
	СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей
		= ПоместитьВоВременноеХранилище(СтруктураМногострочныхЧастей, УникальныйИдентификатор);
	
	Модифицированность = Истина;
	
	Если СтруктураМногострочнойЧасти.Количество() <> 0 Тогда 
		ОтборВТЗ = Новый Структура;
		НовыйМассивПоказателей = Новый Массив;
		
		Для Каждого ЭлемМ Из СтруктураМногострочнойЧасти.Состав[0] Цикл
			НовыйМассивПоказателей.Добавить(ЭлемМ.Ключ + "_" + НомерУдаляемойСтроки);
		КонецЦикла;
		
		Для Каждого ЭлемМассив Из НовыйМассивПоказателей Цикл
			ОтборВТЗ.Вставить("Ячейка",ЭлемМассив);
			
			МассивНайденныхСтрок = ТаблицаКомментарии.НайтиСтроки(ОтборВТЗ);
			
			Для Каждого ЭлемТЗ Из МассивНайденныхСтрок Цикл
				ТаблицаКомментарии.Удалить(ЭлемТЗ);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура РазделыОтчетаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено
		ИЛИ Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	Элементы.ОчиститьТекущуюСтраницу.Доступность = Истина;
	
	ВывестиДанныеВТабличныйДокументНаСервере(
	Элемент.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	
	Если СтруктураРеквизитовФормы.НаимТекущегоРаздела = "Титульный_" Тогда
		ЗаполнениеШапкиНаСервере();
	КонецЕсли;
		
	Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область(1, 1);
		
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы   

 &НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьНаКлиенте(, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
		
	ВставитьДополнительнуюСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	ИмяПоказателя = ТабличныйДокумент.ТекущаяОбласть.Имя;
	ДополнительныеПараметры = Новый Структура("ИмяПоказателя", ИмяПоказателя);
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ТекстВопроса = НСтр("ru='Удалить выбранную строку?'");
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура Проверка(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаЗавершение", ЭтотОбъект);
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
		
КонецПроцедуры

 &НаКлиенте
Процедура ОчиститьТекущуюСтраницу(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Будут очищены все показатели текущего отчета.%1Продолжить операцию?'"), Символы.ПС);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьТекущуюСтраницуЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Будут очищены все показатели текущего отчета.%1Продолжить операцию?'"), Символы.ПС);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьОтчетЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	ДобавитьНовыйКомментарий();
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	//СисИнфо = Новый СистемнаяИнформация;	
	//Массив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СисИнфо.ВерсияПриложения, ".");
	//
	//Если Массив.Количество() >= 3 и Число(Массив[2])<13  Тогда   
	//	Выгрузить_СтарыйВариант();
	//	Возврат;
	//КонецЕсли;
	
	//ОКУД = Прав(ЭтаФорма.Заголовок, 7);
	ОКУД = Лев(Прав(ЭтаФорма.Заголовок, 19),7);
	
	СохранитьДанныеРаздела(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
	#Если НЕ ВебКлиент Тогда
		Режим = РежимДиалогаВыбораФайла.Сохранение; 
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(Режим); 
		ДиалогСохраненияФайла.ПолноеИмяФайла = "ОКУД" + ОКУД; 
		Фильтр = "Файл данных (*.xlsx)|*.xlsx";                 
		ДиалогСохраненияФайла.Фильтр = Фильтр; 
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь; 
		ДиалогСохраненияФайла.Заголовок = "Выберите файл"; 
		
		СписокФайлов = Новый Соответствие;
		
		Если ДиалогСохраненияФайла.Выбрать() Тогда 
			
			Пакет = Новый ПакетОтображаемыхДокументов;
			Пакет.КоличествоЭкземпляров = мСтруктураРазделов.Количество();
			
			Для каждого Элем Из мСтруктураРазделов Цикл	
				
				Если Элем.Ключ = "Комментарии"
					Тогда   
					Продолжить;
				КонецЕсли;
				
				ИмяСтраницы = ?(СтрДлина(Элем.Ключ) <= 31,Элем.Ключ,Лев(Элем.Ключ,31));
			
				ТабличныйДокумент = новый ТабличныйДокумент;			
				ВывестиДанныеВТабличныйДокумент(Элем.Ключ,,,Истина);	
				ЭлементПакета1 = Пакет.Состав.Добавить();
				ЭлементПакета1.Наименование = ИмяСтраницы;
				ЭлементПакета1.Данные = ПоместитьВоВременноеХранилище(ТабличныйДокумент, Новый УникальныйИдентификатор);
				
			КонецЦикла;
			Пакет.Записать(СокрЛП(ДиалогСохраненияФайла.ПолноеИмяФайла),ТипФайлаПакетаОтображаемыхДокументов.XLSX);
			
		КонецЕсли;	
	#Иначе	
		Пакет = Новый ПакетОтображаемыхДокументов;
		Пакет.КоличествоЭкземпляров = мСтруктураРазделов.Количество();
		
		Для каждого Элем Из мСтруктураРазделов Цикл	
			
			Если Элем.Ключ = "Комментарии"
				Тогда   
				Продолжить;
			КонецЕсли;
			ТабличныйДокумент = новый ТабличныйДокумент;			
			ВывестиДанныеВТабличныйДокумент(Элем.Ключ,,,Истина);	
			ЭлементПакета1 = Пакет.Состав.Добавить();
			ЭлементПакета1.Наименование = ?(СтрДлина(Элем.Ключ) <= 31,Элем.Ключ,Лев(Элем.Ключ,31));
			ЭлементПакета1.Данные = ПоместитьВоВременноеХранилище(ТабличныйДокумент, Новый УникальныйИдентификатор);
			
		КонецЦикла;
		Адрес = ЗаписатьПакетНаСервере(Пакет, "ОКУД" + ОКУД + ".xlsx");
		ПолучитьФайл(Адрес, "ОКУД" + ОКУД + ".xlsx", Истина);
		
	#КонецЕсли

	
	ВывестиДанныеВТабличныйДокумент(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаТаблицExcel(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаТаблицExcelПодтверждение", ЭтотОбъект);

	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Будут очищены все показатели текущего отчета.%1Продолжить операцию?'"), Символы.ПС);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек(Команда)
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификаторыТаблиц(Команда)
	
	Если Не МногострочныйРазделВыбран() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не выбран многострочный раздел для автозаполнения идентификаторов. Поставьте курсор на нужную таблицу.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
		
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Сейчас все незаполненные показатели, участвующие в формировании идентификаторов открытых осей, будут заполнены значениями ""НП"" (символом #).%1" + 
	"После этого при формировании пакета XBRL предупреждения о незаполненных показателях, участвующих в формировании идентификаторов открытых осей, выводиться не будут. Пакет XBRL будет сформирован с теми значениями, которые установлены в отчете.%1" + 
	"Нажимая ""Да"", Вы подтверждаете, что согласны с указанными действиями и предупреждены об их последствиях.'"), Символы.ПС);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьИдентификаторыТаблицЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)

	таб_ЗаполнениеОтчетовXBRLКлиент.ЗаполнитьПриНеобходимости(ЭтаФорма,Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Форма.Закрыть();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяПоказателяБезСчетчика(Знач ИмяПоказателя)
	
	ПозицияСчетчика = СтрНайти(ИмяПоказателя, "_");
	
	Если ПозицияСчетчика > 0 Тогда
		Возврат Лев(ИмяПоказателя, ПозицияСчетчика - 1);
	КонецЕсли;
	
	Возврат ИмяПоказателя;
	
КонецФункции

&НаСервере
Процедура ПолучитьСтруктуруМногострочныхЧастей() Экспорт 
	
	Если СтруктураМногострочныхЧастей = Неопределено Тогда
		СтруктураМногострочныхЧастей = ПолучитьИзВременногоХранилища(
			СтруктураРеквизитовФормы.АдресВоВремХранилищеСтруктураМногострочныхЧастей);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаЧГ0(ИсходноеЧисло)
	
	Возврат Формат(ИсходноеЧисло, "ЧГ=0");
	
КонецФункции

&НаСервере
Процедура ПеренумероватьСтрокиИЗаполнитьЗаголовкиМногострочнойЧасти(СтруктураМногострочнойЧасти, ИмяТекРаздела)

	Перем Кодификаторы;
	Перем Заголовки;
	
	Если НЕ СтруктураМногострочнойЧасти.ИмяПоляТабличногоДокумента = ИмяТекРаздела Тогда
		Возврат;
	КонецЕсли;
	
	// Если в описании структуры многострочной части указаны кодификаторы, то производится перенумерация
	Если СтруктураМногострочнойЧасти.Свойство("Кодификаторы", Кодификаторы) Тогда
		Если ТипЗнч(Кодификаторы) = Тип("Массив") Тогда
			Для Каждого Кодификатор Из Кодификаторы Цикл
				
				КоличествоКодов = СтруктураМногострочнойЧасти.Состав.Количество();
				МаксимальноеКоличествоСтрок = Неопределено;
				Если СтруктураМногострочнойЧасти.Свойство("Масштаб")
				   И СтруктураМногострочнойЧасти.Масштаб.Свойство("МаксимальноеКоличествоСтрок", МаксимальноеКоличествоСтрок)
				   И МаксимальноеКоличествоСтрок <> 0 Тогда
					КоличествоКодов = Мин(МаксимальноеКоличествоСтрок, КоличествоКодов);
				КонецЕсли;
				Для Инд = 1 По КоличествоКодов Цикл
					ОбластьСКодом = ТабличныйДокумент.Области[Кодификатор.Область + "_" + СтрокаЧГ0(Инд)];
					ОбластьСКодом.Текст = Формат(Кодификатор.Код + Инд - 1, "ЧГ=0");
				КонецЦикла;
				// Для остальных строк коды не назначаются.
				Для Инд = КоличествоКодов + 1 По СтруктураМногострочнойЧасти.Состав.Количество() Цикл
					ОбластьСКодом = ТабличныйДокумент.Области[Кодификатор.Область + "_" + СтрокаЧГ0(Инд)];
					ОбластьСКодом.Текст = "";
				КонецЦикла;
				
			КонецЦикла;
		ИначеЕсли ТипЗнч(Кодификаторы) = Тип("Структура") Тогда
			Для Инд = 1 По СтруктураМногострочнойЧасти.Состав.Количество() Цикл
				ОбластьСКодом = ТабличныйДокумент.Области[Кодификаторы.Область + "_" + СтрокаЧГ0(Инд)];
				ОбластьСКодом.Текст = Формат(Кодификаторы.Код + Инд - 1, "ЧГ=0");
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	// Если в описании структуры встречаются поля с датами, то производится их заполнение
	Если СтруктураМногострочнойЧасти.Свойство("Заголовки", Заголовки) Тогда
		Если ТипЗнч(Заголовки) = Тип("Массив") Тогда
			Для Каждого ЗаголовокСтроки Из Заголовки Цикл
				Для Инд = 1 По СтруктураМногострочнойЧасти.Состав.Количество() Цикл
					ОбластьОтчета = ТабличныйДокумент.Области[ЗаголовокСтроки.Область + "_" + СтрокаЧГ0(Инд)];
					ОбластьОтчета.Текст = СтруктураРеквизитовФормы.ЗаголовкиОтчетов[ЗаголовокСтроки.Заголовок];
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьАтрибутКакСтроку(Узел, ИмяАтрибута)
	
	Атрибут = Узел.Атрибуты.ПолучитьИменованныйЭлемент(ИмяАтрибута);
	
	Если Атрибут = Неопределено Тогда
		Значение = "";
	Иначе
		Значение = Атрибут.Значение;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Функция ОбъектОтчета(ЭтаФормаИмя) Экспорт
	
	Если мОбъектОтчета = Неопределено Тогда 
		мОбъектОтчета = РегламентированнаяОтчетностьВызовСервера.ОбъектОтчета(ЭтаФормаИмя);
	КонецЕсли;
	
	Возврат мОбъектОтчета;
	
КонецФункции

&НаСервере
Функция ПолучитьАтрибутКакЧисло(Узел, ИмяАтрибута)
	
	Атрибут = Узел.Атрибуты.ПолучитьИменованныйЭлемент(ИмяАтрибута);
	
	Если Атрибут = Неопределено Тогда
		Значение = 0;
	Иначе
		Значение = Число(Атрибут.Значение);
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьСкрытьБлокКомментарии(Элемент)
	Элементы.ГруппаКомментарии.Видимость = Не Элементы.ГруппаКомментарии.Видимость;
	Элементы.Комментарии.Пометка 		 = Не Элементы.Комментарии.Пометка;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СобратьДанныеТекущегоТаблПоля(Форма, ТабличноеПоле) Экспорт

	СтруктураДанныхПоля = Новый Структура;

	ТабличноеПоле = Форма[ТабличноеПоле];
	
	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];

		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;

		ИмяПоказателя      = ТекущаяОбласть.Имя;
		ЗначениеПоказателя = ТекущаяОбласть.Значение;
		СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);

	КонецЦикла;

	Возврат СтруктураДанныхПоля;

КонецФункции

&НаСервере
Процедура ВывестиРазделВТабличныйДокумент(ИдентификаторМногострочнойЧасти)
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	СтруктураМногострочнойЧасти = СтруктураМногострочныхЧастей[ИдентификаторМногострочнойЧасти];
	СтрокВМакете = СтруктураМногострочнойЧасти.Масштаб.МинимальноеКоличествоСтрок;
	КоличествоСтрокВМногострочнойЧасти = СтруктураМногострочнойЧасти.Состав.Количество();
	
	Если СтрокВМакете < КоличествоСтрокВМногострочнойЧасти Тогда
		
		// Удаление имени области многострочной части.
		ИмяОблМнЧ = ИдентификаторМногострочнойЧасти;
		Если СтрНайти(ИдентификаторМногострочнойЧасти, "П00010") = 1 ИЛИ СтрНайти(ИдентификаторМногострочнойЧасти, "П10010") Тогда
			КодСтроки = Сред(ИмяОблМнЧ, 7);
			Пока Лев(КодСтроки, 1) = "0" Цикл
				КодСтроки = Сред(КодСтроки, 2);
			КонецЦикла;
			ИмяОблМнЧ = "ДопСтрока" + КодСтроки;
		КонецЕсли;
		ОблМнЧ = ТабличныйДокумент.Области.Найти(ИмяОблМнЧ);
		Если ОблМнЧ <> Неопределено Тогда
			ИсходнаяОбластьСтрокВерх = ОблМнЧ.Верх;
			ИсходнаяОбластьСтрокНиз  = ОблМнЧ.Низ;
			ОблМнЧ.Имя = "";
		Иначе
			ИсходнаяОбластьСтрокВерх = 0;
			ИсходнаяОбластьСтрокНиз  = 0;
		КонецЕсли;
		
		ИмяВерхнегоЭлементаСтроки = СтруктураМногострочнойЧасти.Габариты.ВерхнийЭлементСтроки + "_" + СтрокаЧГ0(СтрокВМакете);
		ИмяНижнегоЭлементаСтроки  = СтруктураМногострочнойЧасти.Габариты.НижнийЭлементСтроки + "_" + СтрокаЧГ0(СтрокВМакете);
		
		ОбластьВставляемойСтрокиВерх = ТабличныйДокумент.Области[ИмяВерхнегоЭлементаСтроки].Верх;
		ОбластьВставляемойСтрокиНиз  = ТабличныйДокумент.Области[ИмяНижнегоЭлементаСтроки].Низ;
		
		ВысотаСтроки = ОбластьВставляемойСтрокиНиз - ОбластьВставляемойСтрокиВерх + 1;
		
		// Добавление необходимого количества дополнительных строк.
		ОбластьВставляемойСтроки
			= ТабличныйДокумент.Область(ОбластьВставляемойСтрокиВерх, , ОбластьВставляемойСтрокиНиз);
		ОбластьСтрокиПреемника
			= ТабличныйДокумент.Область(ОбластьВставляемойСтрокиНиз + 1, , ОбластьВставляемойСтрокиНиз
			+ (КоличествоСтрокВМногострочнойЧасти - СтрокВМакете) * ВысотаСтроки);
		ТабличныйДокумент.ВставитьОбласть(ОбластьВставляемойСтроки, ОбластьСтрокиПреемника,
			ТипСмещенияТабличногоДокумента.ПоВертикали, Ложь);
		
		// Назначение имени области многострочной части.
		Если ИсходнаяОбластьСтрокВерх <> 0 Тогда
			ОблМнЧ = ТабличныйДокумент.Область(ИсходнаяОбластьСтрокВерх, ,
			ОбластьВставляемойСтрокиНиз + (КоличествоСтрокВМногострочнойЧасти - СтрокВМакете) * ВысотаСтроки);
			Если ОблМнЧ <> Неопределено Тогда
				ОблМнЧ.Имя = ИмяОблМнЧ;
			КонецЕсли;
		КонецЕсли;
		
		// Назначение имен ячеек с показателями в добавленных дополнительных строках.
		Для НомДопСтроки = СтрокВМакете + 1 По КоличествоСтрокВМногострочнойЧасти Цикл
			
			Смещение = ВысотаСтроки * (НомДопСтроки - 1);
			
			ОсноваИмениОблУдаленияСтроки = "";
			
			Для Каждого ЭлементСтруктуры Из СтруктураМногострочнойЧасти.Состав[НомДопСтроки - 1] Цикл
				
				ОсноваИмениПоказателя = ЭлементСтруктуры.Ключ;
				
				ИсходнаяОбластьПоказателя = ТабличныйДокумент.Области[ОсноваИмениПоказателя + "_1"];
				
				ОбластьПоказателяНовойСтроки = ТабличныйДокумент.Область(
				ИсходнаяОбластьПоказателя.Верх + Смещение, ИсходнаяОбластьПоказателя.Лево,
				ИсходнаяОбластьПоказателя.Низ + Смещение, ИсходнаяОбластьПоказателя.Право);
				
				ОбластьПоказателяНовойСтроки.Имя = ОсноваИмениПоказателя + "_" + СтрокаЧГ0(НомДопСтроки);
				
				// Установка значений показателей в добавленных дополнительных строках.
				Если ОбластьПоказателяНовойСтроки.СодержитЗначение Тогда
					ОбластьПоказателяНовойСтроки.Значение = ЭлементСтруктуры.Значение;
				Иначе
					ОбластьПоказателяНовойСтроки.Текст = ЭлементСтруктуры.Значение;
				КонецЕсли;
				
				// Получение основы для последующего назначения имени
				// ячейки со значком удаления дополнительной строки.
				Если ОсноваИмениОблУдаленияСтроки = "" Тогда
					Если Лев(ОсноваИмениПоказателя, 1) = "П" И Прав(ОсноваИмениПоказателя, 2) = "01" Тогда
						ОсноваИмениОблУдаленияСтроки = ОсноваИмениПоказателя;
					КонецЕсли;
				КонецЕсли;
				
			Конеццикла;
			
			// Назначение имени ячейки со значком удаления дополнительной строки.
			Если ОсноваИмениОблУдаленияСтроки <> "" Тогда
				
				ОбластьУдалитьНовая = ТабличныйДокумент.Область(
				ОбластьВставляемойСтрокиВерх + Смещение, 2,
				ОбластьВставляемойСтрокиВерх + Смещение, 2);
				
				ОбластьУдалитьНовая.Имя
				= "УдалитьСтроку" + ОсноваИмениОблУдаленияСтроки + "_" + СтрокаЧГ0(НомДопСтроки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПеренумероватьСтрокиИЗаполнитьЗаголовкиМногострочнойЧасти(СтруктураМногострочнойЧасти, СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
КонецПроцедуры

&НаСервере
Процедура КонвертироватьСтруктуруМногострочныхЧастей(СохрСтруктураМногострочныхЧастей)
	
	Для каждого ЭлемИницСтруктурыМногострочныхЧастей Из СтруктураМногострочныхЧастей Цикл
		Если НЕ СохрСтруктураМногострочныхЧастей.Свойство(ЭлемИницСтруктурыМногострочныхЧастей.Ключ) Тогда
			СохрСтруктураМногострочныхЧастей.Вставить(
				ЭлемИницСтруктурыМногострочныхЧастей.Ключ,
				ЭлемИницСтруктурыМногострочныхЧастей.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНовыйКомментарий()
	
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ТекущаяОбласть;
	
	ИмяТекОбласти = Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя;
	
//	Если Лев(ИмяТекОбласти,2) = "П0" И ТекущаяОбласть.СодержитЗначение Тогда
	Если ТекущаяОбласть.СодержитЗначение Тогда
		
		Если ТаблицаКомментарии.Количество() <> 0 Тогда
			ОтборВТЗ = Новый Структура;
			ОтборВТЗ.Вставить("Ячейка",ИмяТекОбласти);
			МассивНайденныхСтрок = ТаблицаКомментарии.НайтиСтроки(ОтборВТЗ);
			Если МассивНайденныхСтрок.Количество()<> 0 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Для ячейки " + ИмяТекОбласти + " уже добавлен комментарий!";
				Сообщение.Сообщить();
			Иначе
				НоваяСтрока = ТаблицаКомментарии.Добавить();
				НоваяСтрока.Ячейка =  ИмяТекОбласти;
			КонецЕсли;
		Иначе
			НоваяСтрока = ТаблицаКомментарии.Добавить();
			НоваяСтрока.Ячейка =  ИмяТекОбласти;
		КонецЕсли;
		
	Иначе 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось определить показатель, для которого необходимо добавить комментарий!";
		Сообщение.Сообщить();
		
	КонецЕсли;

КонецПроцедуры

 &НаСервереБезКонтекста
Процедура ЗаполнитьСтруктуруИзСтруктуры(СтруктПриемник, СтруктИсточник)
	
	Для Каждого элем Из СтруктИсточник Цикл	
		Если СтруктПриемник.Свойство(элем.Ключ) Тогда
			Если ЗначениеЗаполнено(элем.Значение) Тогда
				СтруктПриемник[элем.Ключ] = элем.Значение;
			КонецЕсли;
		Иначе
			СтруктПриемник.Вставить(элем.Ключ, элем.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаОтчетности

&НаКлиенте
Процедура ПроверкаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТаблицаСообщений.Очистить();
	
	ПроверитьОтчет();
	
	Если ТаблицаСообщений.Количество() Тогда
		
		ОтобразитьФормуНавигацииПоОшибкам();
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Отчет не прошел проверку.'");
		Сообщение.Сообщить();
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Для исправления ошибок используйте окно навигации по ошибкам.'");
		Сообщение.Сообщить();
		
	Иначе
		ПоказатьПредупреждение( , НСтр("ru='Ошибок не обнаружено!'"));		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПроверитьОтчет()
	
	таб_XBRLСервер.ПроверитьОтчет(ЭтаФорма);
		
КонецФункции

&НаКлиенте
Процедура ОтобразитьФормуНавигацииПоОшибкам()
	
	ТаблицаСообщенийСтруктура = Новый Структура("ТаблицаСообщений", ТаблицаСообщений);
	
	ПараметрыФормы = Новый Структура("ВладелецТС", ТаблицаСообщенийСтруктура);
	
	ФормаНавигацииПоОшибкам = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаНавигацииПоОшибкам", ПараметрыФормы);
	
	Если ФормаНавигацииПоОшибкам.Открыта() Тогда
		
		ФормаНавигацииПоОшибкам.Закрыть();
		
		ФормаНавигацииПоОшибкам = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаНавигацииПоОшибкам", ПараметрыФормы);
		
	КонецЕсли;
	
	ФормаНавигацииПоОшибкам.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	РегламентированнаяОтчетностьКлиент.АктивизироватьЯчейку(ЭтаФорма, Ячейка);
	
КонецПроцедуры

#КонецОбласти

#Область Очистка

&НаКлиенте
Процедура ОчиститьТекущуюСтраницуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Нет Тогда
		ОчиститьНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере(НаимТекущегоРаздела = "", ОчищатьНомерКорректировки = Истина)
	
		мСтруктураРазделов[НаимТекущегоРаздела].Очистить();
					
		ОчиститьТабличноеПоле(НаимТекущегоРаздела);
					
		ВывестиДанныеВТабличныйДокументНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела, 1); 
		
		ЗаполнениеШапкиНаСервере();
		
		Модифицированность = Истина;
	
	КонецПроцедуры
	
&НаСервере
Процедура ОчиститьТабличноеПоле(НаимТекущегоРаздела = "")
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	// Определяем содержание многострочных блоков и удаляем все строки кроме первой
	Для Каждого ЭлементСтруктуры Из СтруктураМногострочныхЧастей Цикл
		
		СтруктураМногострочнойЧасти = ЭлементСтруктуры.Значение;
		
		Если ЗначениеЗаполнено(НаимТекущегоРаздела)
			И НЕ СтруктураМногострочнойЧасти.ИмяПоляТабличногоДокумента = НаимТекущегоРаздела Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураГруппы = СтруктураМногострочнойЧасти.Состав;
		
		КопияСтруктураГруппы = СтруктураГруппы[0];
		
		// очищаем структуру группы и воссоздаем строку по умолчанию
		СтруктураГруппы.Очистить();
		СтруктураГруппы.Добавить(КопияСтруктураГруппы);
		
	КонецЦикла;
	
	Для Инд = 0 По ТабличныйДокумент.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличныйДокумент.Области[Инд];

		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если ТекущаяОбласть.СодержитЗначение = Неопределено ИЛИ НЕ ТекущаяОбласть.СодержитЗначение Тогда	
			Продолжить;
		КонецЕсли;

		Если ТекущаяОбласть.Защита <> Неопределено И ТекущаяОбласть.Защита Тогда
			Продолжить;
		КонецЕсли;

		ТекущаяОбласть.Очистить();
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчетЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьОтчетНаСервере();
	
	//ЗаполнитьПодписиВопрос();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьОтчетНаСервере()
	
	ИнициализироватьРазделы();
	
	ИнициализироватьОписанияМногострочныхЧастей();
	
	ВывестиДанныеВТабличныйДокумент(СтруктураРеквизитовФормы.НаимТекущегоРаздела, 1);
	
	ЗаполнениеШапкиНаСервере();

	Модифицированность = Истина;
	
КонецПроцедуры


#КонецОбласти
          
#Область ЗаполнениеШапки

&НаКлиенте
Процедура ЗаполнениеШапкиНаКлиенте(ИмяРаздела)
		
	Если ИмяРаздела = "Титульный"  Тогда	
		ЗаполнениеШапкиНаСервере();
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ЗаполнениеШапкиНаСервере()
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
    // << таб_#111292 	
	Если СтруктураРеквизитовФормы.СфераПрименения <> Неопределено И СтруктураРеквизитовФормы.СфераПрименения <> ""  Тогда
		ТитульныйЛист = "Титульный" + "_" + СтруктураРеквизитовФормы.СфераПрименения;
	Иначе
		ТитульныйЛист = "Титульный";
	КонецЕсли;	
	// >> таб_#111292 
	
	таб_ОтчетностьСлужебный.ЗаполнениеШапкиНаСервереБезКонтекста(СтруктураРеквизитовФормы, ТабличныйДокумент, мСтруктураРазделов[ТитульныйЛист]); 
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСExcel

&НаСервере
Функция ЗаписатьПакетНаСервере(Пакет, ИмяФайла)
	
	Пакет.Записать(КаталогВременныхФайлов() + СокрЛП(ИмяФайла),ТипФайлаПакетаОтображаемыхДокументов.XLSX);
	ФайлыЭксель = НайтиФайлы(КаталогВременныхФайлов(), СокрЛП(ИмяФайла), Ложь);
	ДвоичныеДанные = Новый ДвоичныеДанные(ФайлыЭксель[0].ПолноеИмя);
	Идентификатор  = Новый УникальныйИдентификатор;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Идентификатор); 
	УдалитьФайлы(ФайлыЭксель[0].ПолноеИмя);
	Возврат АдресХранилища;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаТаблицExcelПодтверждение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда 	
		Попытка 
			ПодключитьРасширениеРаботыСФайлами();
		Исключение
		КонецПопытки;
	#КонецЕсли	
	
	ТаблицаСообщений.Очистить();
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	Диалог.Заголовок = "Выберите файлы для загрузки"; 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаТаблицExcelЗавершение", ЭтотОбъект);
	
	НачатьПомещениеФайлов(ОписаниеОповещения, Диалог, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаТаблицExcelЗавершение(МассивФайлов, ДопПараметры) Экспорт

	ИмяТекРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	ЗагрузкаТаблицExcelЗавершениеНаСервере(МассивФайлов, ДопПараметры, ИмяТекРаздела);	
	
	СообщитьОбУспешнойЗагрузкеExcel();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбУспешнойЗагрузкеExcel();
	
	Если СтруктураРеквизитовФормы.Свойство("ИмяЗагруженногоФайла") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = таб_ОтчетностьСлужебный.СообщитьОбУспешнойЗагрузкеExcel(СтруктураРеквизитовФормы.ИмяЗагруженногоФайла);
		Сообщение.Сообщить();
		СтруктураРеквизитовФормы.Удалить("ИмяЗагруженногоФайла");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаТаблицExcelЗавершениеНаСервере(МассивФайлов, ДопПараметры, ИмяТекРаздела)

	Если МассивФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьОтчетНаСервере();
	
	ЗаполнениемСтруктураРазделовExcel();
	
	таб_ОтчетностьСлужебный.ЗагрузкаТаблицExcel(МассивФайлов, мСтруктураРазделов, мСтруктураРазделовExcel, СтруктураМногострочныхЧастей, СтруктураРеквизитовФормы);
		
	//Если СтруктураРеквизитовФормы.ПрименятьПоискКонтрагентовПослеЗагрузкиИзExcel Тогда
	//	таб_ЗаполнениеОтчетовXBRLСервер.НайтиКонтрагентовПослеЗагрузкиДанных(ЭтаФорма);
	//КонецЕсли;
	
	ВывестиДанныеВТабличныйДокумент(ИмяТекРаздела);
	
	// #TODO: Реализовать расчет итогов.
	// РасчетНаСервере();
	
	ЗаполнениеШапкиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениемСтруктураРазделовExcel()
	
	ПолучитьСтруктуруМногострочныхЧастей();
	
	таб_ОтчетностьСлужебный.ЗаполнениемСтруктураРазделовExcel(мСтруктураРазделов, мСтруктураРазделовExcel, ЭтаФорма.ИмяФормы, СтруктураМногострочныхЧастей);
	
КонецПроцедуры

#КонецОбласти

#Область ФормаНастроек

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
	
	Перем ТекущийРазделОтчетаСокрНаим;
	Перем НомерСтраницыРазделаОтчета;
	
	мПараметры = Новый Структура;
	мПараметры.Вставить("ПроверкаСоотношений", Ложь);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("СчетчикСтраниц", Ложь);
	мПараметры.Вставить("ЗначенияИзСписка", Ложь);
	//мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЕдиницаИзмерения", СтруктураРеквизитовФормы.ЕдиницаИзмерения);
	ПараметрыФормы.Вставить("ТочностьЕдиницыИзмерения", СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
	ПараметрыФормы.Вставить("ИспользоватьИндивидуальныеНастройки", СтруктураРеквизитовФормы.ИспользоватьИндивидуальныеНастройки);
	ПараметрыФормы.Вставить("ПереноситьЗначенияВЯчейках", СтруктураРеквизитовФормы.ПереноситьЗначенияВЯчейках);

	// Параметр формы "Дополнительные настройки" позволяет добавить на форму настроек дополнительные поля 
	// для изменения реквизитов примитивных типов из СтруктураРеквизитовФормы.
	// Тип значения - Структура, в которой ключ соответствует ключу реквизита из СтруктураРеквизитовФормы, 
	// а значение - Структура со свойствами "Заголовок" (строка) и "ТекЗначение" (текущее значение реквизита)
	// ПроверятьИНН = Новый Структура;
	// ПроверятьИНН.Вставить("Заголовок", "Проверка на наличие нецифровых символов в ИНН и TIN");
	// ПроверятьИНН.Вставить("ТекЗначение", СтруктураРеквизитовФормы.ПроверятьИНН); 
	// ПараметрыФормы.Вставить("ДополнительныеНастройки", Новый Структура("ПроверятьИНН", ПроверятьИНН));
	ФормаНастройкиОтчета = ПолучитьФорму("Отчет.таб_XBRL.Форма.СквозныеНастройкиОтчетов", ПараметрыФормы, ЭтаФорма);

	ЗначениеЕдИзм = 				СтруктураРеквизитовФормы.ЕдиницаИзмерения;
	ЗначениеЗпт   = 				СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	
	ДополнительныеПараметры = Новый Структура("ЗначениеЕдИзм, ЗначениеЗпт, ОтрицательныеСуммыВСкобках ", ЗначениеЕдИзм, ЗначениеЗпт);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеЕдИзм = ДополнительныеПараметры.ЗначениеЕдИзм;
	ЗначениеЗпт = ДополнительныеПараметры.ЗначениеЗпт;
	
	Модифицированность = Истина;
	
	Если ЗначениеЕдИзм <> СтруктураРеквизитовФормы.ЕдиницаИзмерения
		ИЛИ ЗначениеЗпт <> СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения Тогда
		ПослеСменыЕдИзмеренияИТочности();
	КонецЕсли;
	
	ВывестиДанныеВТабличныйДокументНаСервере(СтруктураРеквизитовФормы.НаимТекущегоРаздела);
	
КонецПроцедуры

&НаСервере
Процедура ПослеСменыЕдИзмеренияИТочности()
	
	таб_XBRLОбщегоНазначения.ПроверитьТочность(ЭтаФорма);
	таб_XBRLОбщегоНазначения.УстановитьФорматВыводаНаСервере(ЭтаФорма);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

//<< таб_#36893 
#Область ЗаполнениеИдентификаторовТаблиц 

&НаКлиенте
Процедура ЗаполнитьИдентификаторыТаблицЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ТекущаяОбласть;
	Если ТекущаяОбласть = неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоказателя = ИмяПоказателяБезСчетчика(ТекущаяОбласть.Имя);
	
	// Идентификатор многострочной части должен совпадать с именем области многострочной части в таблице.
	ИдентификаторМногострочнойЧасти = СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию[ИмяПоказателя];
	
	ЗаполнитьИдентификаторыТаблицыНаСервере(ИдентификаторМногострочнойЧасти);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыТаблицыНаСервере(ИдентификаторМногострочнойЧасти)
	
	ИсточникОтчета = Метаданные.Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].Имя;
	СтруктураРеквизитовФормы.Вставить("СтруктураИдентификаторовТаблиц", таб_XBRLОбщегоНазначения.ПолучитьСтруктураИдентификаторовТаблиц(ИсточникОтчета, СтруктураРеквизитовФормы));
	
	Если СтруктураРеквизитовФормы.СтруктураИдентификаторовТаблиц.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ПолучитьСтруктуруМногострочныхЧастей();
		
		ОписаниеМногострочнойЧасти = СтруктураМногострочныхЧастей[ИдентификаторМногострочнойЧасти];
		
	Исключение
		
		Возврат;
		
	КонецПопытки;
	
	Если Не СтруктураРеквизитовФормы.СтруктураИдентификаторовТаблиц.Свойство(ИдентификаторМногострочнойЧасти) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоСтрокВМногострочнойЧасти = ОписаниеМногострочнойЧасти.Состав.Количество();
	НаимРаздела = СтруктураРеквизитовФормы.НаимТекущегоРаздела;
	мСтруктураРазделов[НаимРаздела] = СобратьДанныеТекущегоТаблПоля(ЭтаФорма, "ТабличныйДокумент");
	
	таб_XBRLОбщегоНазначения.ЗаполнитьПустыеИдентификаторыТаблицы(мСтруктураРазделов, СтруктураРеквизитовФормы, ИдентификаторМногострочнойЧасти, КоличествоСтрокВМногострочнойЧасти);
	
	ВывестиДанныеВТабличныйДокумент(НаимРаздела);
	
КонецПроцедуры

&НаСервере
Функция МногострочныйРазделВыбран()
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ТекущаяОбласть;
	Если ТекущаяОбласть = неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяПоказателя = ИмяПоказателяБезСчетчика(ТекущаяОбласть.Имя);
	
	// Идентификатор многострочной части должен совпадать с именем области многострочной части в таблице.
	ИдентификаторМногострочнойЧасти = СтруктураРеквизитовФормы.СоответствиеПоказателейМногострочныхЧастейИхОписанию[ИмяПоказателя];
	
	Попытка
		
		ПолучитьСтруктуруМногострочныхЧастей();
		
		ОписаниеМногострочнойЧасти = СтруктураМногострочныхЧастей[ИдентификаторМногострочнойЧасти];
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
//>> таб_    

#Область Заполнение

////////////////////////////////////////////////////////////////////////////////
//
// Процедура ПодготовитьПараметрыЗаданияЗаполнения
//
// Описание: 
//	Заполняет индивидуальные для текущей формы параметры, необходимые для работы задания заполнения.
//
// Параметры:
//	ПараметрыЗаполнения - Структура - Входящие параметры, которые при необходимости могут быть дополнены индивидуальными
//	
&НаКлиенте
Процедура ПодготовитьПараметрыЗаданияЗаполнения(ПараметрыЗаполнения) Экспорт
		
	ПараметрыЗаполнения.Вставить("ДополнительныеПараметры", Новый Структура);
	ПараметрыЗаполнения.Вставить("ДанныеЗаполнения", Неопределено);
	
	ПараметрыЗаполнения.ДополнительныеПараметры.Вставить(
		"мСтруктураРазделов", мСтруктураРазделов);
	
	ПараметрыЗаполнения.ДополнительныеПараметры.Вставить(
		"СтруктураРеквизитовФормы", СтруктураРеквизитовФормы);
	
	ПараметрыЗаполнения.Вставить("ВыполнятьВФоне", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыполненияЗаданияЗаполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(Результат) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Результат.КраткоеПредставлениеОшибки);
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		ЗавершениеВыполненияЗаданияЗаполненияСервер(Результат, ДополнительныеПараметры);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗавершениеВыполненияЗаданияЗаполненияСервер(Результат, ДополнительныеПараметры)
	
	таб_ЗаполнениеОтчетовXBRLСервер.ЗавершениеВыполненияЗаданияЗаполненияСервер(
		ЭтаФорма, Результат, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти
